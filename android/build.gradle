apply plugin: 'com.android.application'

import org.apache.tools.ant.taskdefs.condition.Os

def fetchPassword(identifier) {
    if (!Os.isFamily(Os.FAMILY_MAC)) {
        return props.getProperty(identifier)
    }
    def account = "remiges"
    def keychainLabel = "remiges-release-${identifier}"
    println "Fetching keychain password with account '${account}' and label '${keychainLabel}"
    def stdout = new ByteArrayOutputStream()
    def stderr = new ByteArrayOutputStream()
    def keychainExec = exec {
        commandLine 'security', '-q', 'find-generic-password', '-a', account, '-gl', "${keychainLabel}"
        standardOutput = stdout
        errorOutput = stderr
        ignoreExitValue true
    }
    if (keychainExec.exitValue != 0) {
        println stdout.toString()
        println stderr.toString()
        return
    }
    (stderr.toString().trim() =~ /password: "(.*)"/)[0][1]
}

def props = new Properties()
props.load(new FileInputStream(file("release.properties")))

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    defaultConfig {
        applicationId 'org.tomcurran.remiges'
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 1
        versionName '1.0'
        multiDexEnabled true
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile file(props.keyStore)
            storePassword fetchPassword('keyStorePassword')
            keyAlias props.keyAlias
            keyPassword fetchPassword('keyAliasPassword')
        }
    }

    buildTypes {
        debug {
            debuggable true
        }
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile "com.android.support:support-v4:${android_support_lib_version}"
    compile "com.android.support:appcompat-v7:${android_support_lib_version}"
    compile "com.android.support:design:${android_support_lib_version}"
    compile 'com.android.support:multidex:1.0.1'
    compile "com.google.android.gms:play-services-drive:${google_play_services_client_library_version}"
    compile "com.google.android.gms:play-services-maps:${google_play_services_client_library_version}"
    compile "com.google.android.gms:play-services-location:${google_play_services_client_library_version}"
    compile 'com.google.maps.android:android-maps-utils:0.4'
    compile 'com.google.code.gson:gson:2.4'
    compile 'se.emilsjolander:stickylistheaders:2.7.0'
    compile fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.1'
}
